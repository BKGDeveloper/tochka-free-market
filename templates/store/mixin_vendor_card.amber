mixin vendorCard($seller, $localization)
  if !IsSingleMode
    div.ui.card
      div.image
        img[src="/user-avatar/" + $seller.Username]
      div.content
        a.header[href="/user/"+$seller.Username] 
          if $seller.IsOnline
            i.icon.dark-green.circle
          $lang=$seller.Language
          if $lang == "en"
            i.flag.us
          else
            i.flag[class=$lang]
          | @#{$seller.Username}
        div.header
          if $seller.IsStaff
            div.ui.label.tiny #{$localization.UserCard.Staff}
          if $seller.IsSeller
            if $seller.IsTrustedSeller
              div.ui.orange.label.tiny.basic
                i.icon.star
                | #{$localization.UserCard.TrustedSeller}
            if $seller.PremiumPlus
              div.ui.teal.label.tiny #{$localization.UserCard.PremiumVendor}+
            else if $seller.Premium
              div.ui.dark-green.label.tiny #{$localization.UserCard.PremiumVendor}
            else
              div.ui.label.tiny #{$localization.UserCard.Vendor}
          else
            div.ui.label.tiny #{$localization.UserCard.Buyer}
            if $seller.IsTester
              div.ui.dark-green.label.tiny #{$localization.UserCard.Tester}
        div.meta
          div.date 
            i.icon.wait
            span #{$localization.UserCard.Registred} #{$seller.RegistrationDateStr}
          div.date 
            i.icon.wait
            span #{$localization.UserCard.LastSeen} #{$seller.LastLoginDateStr}
        if $seller.Description != ""
          div.description #{$seller.Description}
      if $seller.IsSeller && ($seller.Score > 0 || $seller.MultisigEnabled || $seller.PossibleScammer)
        div.extra.content
          if $seller.Score > 0
            div.ui.label
              i.icon.thumbs.up
              span #{$seller.Score} / 5
          if $seller.MultisigEnabled
            div.ui.label 
              i.icon.bitcoin
              span Multisig
          if $seller.PossibleScammer
            div.ui.red.label #{$localization.UserCard.PossibleScammer}
        if $seller.NumberOfSalesStr != "" && $seller.SalesVolumeStr != ""
          div.extra.content
            div.date 
              i.icon.bitcoin
              span #{$localization.Items.SalesAmount}: #{$seller.NumberOfSalesStr}
            div.date 
              i.icon.bitcoin
              span #{$localization.Items.SalesVolume}: #{$seller.SalesVolumeStr}
      div.extra.content
        a.ui.button.dark-green.tiny[href="/messages/"+$seller.Username]
          i.icon.comment
          | #{$localization.LeftMenu.PM}
  else
    div.ui.card
      div.image
        img[src="/user-avatar/" + $seller.Username]
      div.content
        a.header[href="/user/"+$seller.Username] 
          if $seller.IsOnline
            i.icon.dark-green.circle
          | @#{$seller.Username}
      div.meta
        if $seller.Description != ""
          div.description #{$seller.Description}
      div.extra.content
        a.ui.button.dark-green.tiny[href="/messages/"+$seller.Username]
          i.icon.comment
          | #{$localization.LeftMenu.PM}    